buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.diffplug.spotless' version '6.25.0'
}

apply plugin: 'org.spongepowered.mixin'

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--offline'
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', mod_id,
                    '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/'),
                    '--existing-mod', 'gtceu',
                    '--existing-mod', 'ae2',
                    '--existing-mod', 'mekanism'
        }
    }
}

mixin {
    add sourceSets.main, "sftcore.refmap.json"

    config "sftcore.mixins.json"

    debug = true
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.minecraftforge.net/' }
    maven {
        name = "BMCLAPI"
        url = "https://bmclapi2.bangbang93.com/maven/"
    }
    maven {
        // GregTech
        name = 'GTCEu Maven'
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
        }
    }
    maven {
        // JEI
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // EMI
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        // Registrate
        url "https://maven.tterrag.com/"
    }
    maven {
        // ForgeGradle
        name "firstdarkdev"
        url "https://maven.firstdarkdev.xyz/snapshots"
    }
    maven {
        // Modrinth maven
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        // Curseforge maven
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // Cloth Config API
        url "https://maven.shedaniel.me/"
    }
    maven {
        // Jade
        url = "https://api.modrinth.com/maven"
    }
    maven {
        // Create, Ponder, Flywheel
        url = "https://maven.createmod.net"
    }
    maven {
        // ForgeConfigAPIPort
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven {
        // GuideMe
        name = 'GuideME Snapshots'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
        content {
            includeModule('org.appliedenergistics', 'guideme')
        }
    }
    maven {
        // GeckoLib
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        // Extended Crafting
        url 'https://maven.blakesmods.com'
    }
    maven {
        // LibX
        name = 'ModdingX Maven'
        url = 'https://maven.moddingx.org/'
    }
    mavenLocal()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("com.lowdragmc.ldlib:ldlib-forge-${minecraft_version}:1.0.41.b") { transitive = false }
    implementation fg.deobf("com.gregtechceu.gtceu:gtceu-${minecraft_version}:${gtm_version}")
    implementation fg.deobf("mekanism:Mekanism:${mekanism_version}")
    implementation fg.deobf("mekanism:Mekanism:${mekanism_version}:generators")
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:11.1.136")
    implementation fg.deobf("curse.maven:mega-cells-622112:6175008")
    implementation(fg.deobf("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false })
    implementation(fg.deobf("net.createmod.ponder:Ponder-Forge-${minecraft_version}:${ponder_version}"))
    implementation fg.deobf("curse.maven:glodium-957920:5226922")
    implementation fg.deobf("org.appliedenergistics:guideme:${guideme_version}")
    implementation fg.deobf("appeng:appliedenergistics2-forge:${ae2_version}")
    implementation fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    implementation fg.deobf("dev.emi:emi-forge:${emi_version}")
    implementation fg.deobf("curse.maven:ex-pattern-provider-892005:6677843")
    implementation fg.deobf("curse.maven:ae2addonlib-1366135:7159767")
    implementation fg.deobf("curse.maven:advancedae-1084104:7159779")
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
    implementation fg.deobf("curse.maven:gtmthings-1104310:6843500")
    implementation fg.deobf("curse.maven:jade-324717:6855440")
    implementation fg.deobf("com.blakebr0.cucumber:Cucumber:${minecraft_version}-${cucumber_version}")
    implementation fg.deobf("com.blakebr0.extendedcrafting:ExtendedCrafting:${minecraft_version}-${extended_crafting_version}")
    implementation fg.deobf("curse.maven:travel-anchors-413234:4683662")
    implementation fg.deobf("curse.maven:carry-on-274259:4882500")
    implementation fg.deobf("org.moddingx:LibX:${minecraft_version}-${libx_version}")
    implementation("com.eliotlash.mclib:mclib:20")
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0")) {
        jarJar.ranged(it, "[0.5.0,)")
    }

    compileOnly 'org.projectlombok:lombok:1.18.30'
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    compileOnly fg.deobf("com.tterrag.registrate:Registrate:${registrate_version}")
    compileOnly(fg.deobf("dev.engine-room.flywheel:flywheel-forge-api-${minecraft_version}:${flywheel_version}"))

    runtimeOnly(fg.deobf("dev.engine-room.flywheel:flywheel-forge-${minecraft_version}:${flywheel_version}"))
    runtimeOnly fg.deobf("curse.maven:just-enough-characters-250702:6680042")
    runtimeOnly fg.deobf("curse.maven:embeddium-908741:5681725")
    runtimeOnly fg.deobf("maven.modrinth:jade:${jade_version}")
    runtimeOnly fg.deobf("curse.maven:architectury-api-419699:5137938")
    runtimeOnly fg.deobf("curse.maven:ftb-teams-forge-404468:6130786")
    runtimeOnly fg.deobf("curse.maven:ftb-library-forge-404465:6807424")

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    annotationProcessor fg.deobf("org.moddingx:LibX:${minecraft_version}-${libx_version}")
    annotationProcessor ("io.github.llamalad7:mixinextras-common:0.5.0")
    annotationProcessor "io.github.llamalad7:mixinextras-common:0.4.1"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching([
            'META-INF/mods.toml',
            'pack.mcmeta'
    ]) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

spotless {
    format 'misc', {
        target '.gitignore'

        trimTrailingWhitespace()
        endWithNewline()
    }

    java {
        target 'src/main/java/**/*.java', 'src/test/java/**/*.java'

        def orderFile = file("$rootDir/spotless/spotless.importorder")
        def formatFile = file("$rootDir/spotless/spotless.eclipseformat.xml")

        toggleOffOn()
        importOrderFile(orderFile)
        removeUnusedImports('cleanthat-javaparser-unnecessaryimport')
        endWithNewline()
        eclipse('4.31').configFile(formatFile)
    }
}

